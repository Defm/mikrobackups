:local sysname [/system identity get name]
:local sysver [/system package get system version]
:local scriptname "doBackup"
:local saveSysBackup true
:local encryptSysBackup false
:local saveRawExport true
:local verboseRawExport false
:local state ""

:local ts [/system clock get time]
:set ts ([:pick $ts 0 2].[:pick $ts 3 5].[:pick $ts 6 8])
:local ds [/system clock get date]
:set ds ([:pick $ds 7 11].[:pick $ds 0 3].[:pick $ds 4 6])

#directories have to exist!
:local FTPEnable true
:local FTPServer "minialx.home"
:local FTPPort 21
:local FTPUser "ftp"
:local FTPPass ""
:local FTPRoot "/pub/"
:local FTPGitEnable true
:local FTPRawGitName "/pub/git/rawconf_$sysname_$sysver.rsc"

:local SMTPEnable true
:local SMTPAddress "defm.kopcap@gmail.com"
:local SMTPSubject ("$sysname Full Backup ($ds-$ts)")
:local SMTPBody ("$sysname full Backup file see in attachment.\nRouterOS version: $sysver\nTime and Date stamp: ($ds-$ts) ")

:local noteMe do={
  :put "DEBUG: $value"
  :log info message="$value"
}

:local itsOk true;

:do {
  :local smtpserv [:resolve "$FTPServer"];
} on-error={ 
  :set state "FTP server looks like to be unreachable"
  $noteMe value=$state;
  :set itsOk false;
}

:local fname ("BACKUP-$sysname-$ds-$ts")

:if ($saveSysBackup and $itsOk) do={
  :if ($encryptSysBackup = true) do={ /system backup save name=($fname.".backup") }
  :if ($encryptSysBackup = false) do={ /system backup save dont-encrypt=yes name=($fname.".backup") }
  $noteMe value="System Backup Finished"
}

:if ($saveRawExport and $itsOk) do={
  :if ($FTPGitEnable ) do={
     :if ($verboseRawExport = true) do={ /export terse hide-sensitive verbose file=($fname.".safe.rsc") }
     :if ($verboseRawExport = false) do={ /export terse hide-sensitive  file=($fname.".safe.rsc") }
  }
  $noteMe value="Raw configuration script export Finished"
}

:delay 5s

:local buFile ""

:foreach backupFile in=[/file find] do={
  :set buFile ([/file get $backupFile name])
  :if ([:typeof [:find $buFile $fname]] != "nil" and $itsOk) do={
    :local itsSRC ( $buFile ~".safe.rsc")
    if ($FTPEnable and $itsOk) do={
        :do {
        :local state "Uploading $buFile to FTP ($FTPRoot$buFile)"
        $noteMe value=$state
        /tool fetch address=$FTPServer port=$FTPPort src-path=$buFile user=$FTPUser password=$FTPPass dst-path="$FTPRoot$buFile" mode=ftp upload=yes
        $noteMe value="Done"
        } on-error={ 
          :set state "Error When $state"
          $noteMe value=$state;
          :set itsOk false;
       }

        #special ftp upload for git purposes
        if ($itsSRC and $FTPGitEnable and $itsOk) do={
            :do {
            :local state "Uploading $buFile to FTP (RAW, $FTPRawGitName)"
            $noteMe value=$state
            /tool fetch address=$FTPServer port=$FTPPort src-path=$buFile user=$FTPUser password=$FTPPass dst-path="$FTPRawGitName" mode=ftp upload=yes
            $noteMe value="Done"
            } on-error={ 
              :set state "Error When $state"
              $noteMe value=$state;
              :set itsOk false;
           }
        }

    }
    if ($SMTPEnable and !$itsSRC and $itsOk) do={
        :do {
        :local state "Uploading $buFile to SMTP"
        $noteMe value=$state
        /tool e-mail send to=$SMTPAddress body=$SMTPBody subject=$SMTPSubject file=$buFile
        $noteMe value="Done"
        } on-error={ 
          :set state "Error When $state"
          $noteMe value=$state;
          :set itsOk false;
       }
    }

    :delay 1s;
    /file remove $backupFile;

  }
}

:local inf ""
:if ($itsOk) do={
  :set inf "$scriptname on $sysname: Automatic Backup Completed Successfully"
}

:if (!$itsOk) do={
  :set inf "Error When $scriptname on $sysname: $state"  
}

$noteMe value=$inf

:global TelegramMessage "$inf";

/system script run doTelegramNotify;

