:global globalScriptBeforeRun;
$globalScriptBeforeRun "doBackup";

:local sysname [/system identity get name];
:local rosVer [:tonum [:pick [/system resource get version] 0 1]]

:local sysver "NA"
:if ( [ :len [ /system package find where name="system" and disabled=no ] ] > 0 and $rosVer = 6 ) do={
  :set sysver [/system package get system version]
}
:if ( [ :len [ /system package find where name="routeros" and disabled=no ] ] > 0 and $rosVer = 7 ) do={
  :set sysver [/system package get routeros version]
}

:global globalNoteMe;
:global globalCallFetch;
:global simplercurrdatetimestr;


:local scriptname "doBackup"
:local saveSysBackup true
:local encryptSysBackup false
:local saveRawExport true
:local verboseRawExport false
:local state ""

#directories have to exist!
:local FTPEnable true;
:local FTPServer "nas.home";
:local FTPPort 2022;
:local FTPUser "git";
:local FTPPass "git";
:local FTPRoot "REPO/backups/";
:local FTPGitEnable true;
:local FTPRawGitName "REPO/raw/rawconf_$sysname_latest.rsc";

:local sysnote [/system note get note];

:local stamp [$simplercurrdatetimestr];

:local SMTPEnable true;
:local SMTPAddress "defm.kopcap@gmail.com";
:local SMTPSubject ("$sysname Full Backup ($stamp)");
:local SMTPBody ("$sysname full Backup file see in attachment.\n $sysnote");
:local itsOk true;

:do {
  :local smtpserv [:resolve "$FTPServer"];
} on-error={ 
  :set state "FTP server looks like to be unreachable"
  $globalNoteMe value=$state;
  :set itsOk false;
}


:local fname ("BACKUP-$sysname-$stamp")

:if ($saveSysBackup and $itsOk) do={
  :if ($encryptSysBackup = true) do={ /system backup save name=($fname.".backup") }
  :if ($encryptSysBackup = false) do={ /system backup save dont-encrypt=yes name=($fname.".backup") }
  :delay 2s;
  $globalNoteMe value="System Backup Finished"
}

:if ($saveRawExport and $itsOk) do={
  :if ($FTPGitEnable ) do={
     # show sensitive data
     :if ($verboseRawExport = true) do={ /export show-sensitive terse verbose file=($fname.".safe.rsc") }
     :if ($verboseRawExport = false) do={ /export show-sensitive terse file=($fname.".safe.rsc") }
     :delay 2s;
  }
  $globalNoteMe value="Raw configuration script export Finished"
}

:delay 5s

:local buFile ""

:foreach backupFile in=[/file find] do={
  
  :set buFile ([/file get $backupFile name])
  
  :if ([:typeof [:find $buFile $fname]] != "nil") do={
    
    :local itsSRC ( $buFile ~".safe.rsc")
    
     if ($FTPEnable) do={
        :do {
        :set state "Uploading $buFile to FTP ($FTPRoot$buFile)"
        $globalNoteMe value=$state
 
        :local dst "$FTPRoot$buFile";
        :local fetchCmd "/tool fetch url=sftp://$FTPServer:$FTPPort/$dst src-path=$buFile user=$FTPUser password=$FTPPass upload=yes"

        $globalCallFetch $fetchCmd;

        $globalNoteMe value="Done"

        } on-error={ 
          :set state "Error When $state"
          $globalNoteMe value=$state;
          :set itsOk false;
       }

        #special ftp upload for git purposes
        if ($itsSRC and $FTPGitEnable) do={
            :do {
            :set state "Uploading $buFile to GIT-FTP (RAW, $FTPRawGitName)"
            $globalNoteMe value=$state

            :local dst "$FTPRawGitName";
            :local fetchCmd "/tool fetch url=sftp://$FTPServer:$FTPPort/$dst src-path=$buFile user=$FTPUser password=$FTPPass upload=yes"
 
        $globalCallFetch $fetchCmd;

            $globalNoteMe value="Done"
            } on-error={ 
              :set state "Error When $state"
              $globalNoteMe value=$state;
              :set itsOk false;
           }
        }

    }
    if ($SMTPEnable and !$itsSRC) do={
        :do {
        :set state "Uploading $buFile to SMTP"
        $globalNoteMe value=$state

        #email works in background, delay needed
        /tool e-mail send to=$SMTPAddress body=$SMTPBody subject=$SMTPSubject file=$buFile tls=starttls

        #waiting for email to be delivered
        :delay 15s;

        :local emlResult ([/tool e-mail get last-status] = "succeeded")

        if (!$emlResult) do={

          :set state "Error When $state"
          $globalNoteMe value=$state;
          :set itsOk false;

        } else={

          $globalNoteMe value="Done"
       
        }

        } on-error={ 
          :set state "Error When $state"
          $globalNoteMe value=$state;
          :set itsOk false;
       }
    }

    :delay 2s;
    #/file remove $backupFile;

  }
}

:local inf ""
:if ($itsOk) do={
  :set inf "$scriptname on $sysname: Automatic Backup Completed Successfully"
}

:if (!$itsOk) do={
  :set inf "Error When $scriptname on $sysname: $state"  
}

$globalNoteMe value=$inf

:if (!$itsOk) do={

  :global globalTgMessage;
  $globalTgMessage value=$inf;
  
}



