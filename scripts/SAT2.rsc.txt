#--------------------------------------------------------------------------------------------------------------------------
# SATELLITE2 module  for TLGRM version 2.2 by Sertik (Serkov S.V.) 25/10/2022
#--------------------------------------------------------------------------------------------------------------------------

# declare functions:

:global health
:global report
:global lease
:global dhcpclient
:global wificonnect
:global users
:global log
:global logreset
:global pingpong
:global FuncPing
:global mailsend
:global smssend
:global modeminfo
:global satclear


:set health do={
:if ([:len $0]!=0) do={

# Script view health of device by Enternight
# https://forummikrotik.ru/viewtopic.php?t=7924
# tested on ROS 6.49.5
# updated 2022/04/21

:global Emoji
:global FuncTelegramSender

:do {
  :local hddTotal [/system resource get total-hdd-spac];
  :local hddFree  [/system resource get free-hdd-space];
  :local badBlock [/system resource get bad-blocks    ];
  :local memTotal [/system resource get total-memory  ];
  :local memFree  [/system resource get free-memory   ];
  :local cpuA     [/system resource get cpu];
  :local arhA      [/system resource get arch];
  :local cpuZ     [/system resource get cpu-load      ];
  :local currFW   [/system routerbo get upgrade-firmwa];
  :local upgrFW   [/system routerbo get current-firmwa];
   :if ([/system resource get board-name]!="CHR") do={
         :local tempC    0;
         :local volt     0;
          }
  :local smplVolt ($volt/10);
  :local lowVolt  (($volt-($smplVolt*10))*10);
  :local inVolt   ("$smplVolt.$[:pick $lowVolt 0 3]");
  :set   hddFree  ($hddFree/($hddTotal/100));
  :set   memFree  ($memFree/($memTotal/100));
  :local message  ("$Emoji $[system identity get name] Health report:");
  :set   message  ("$message %0AModel $[system resource get board-name]");
  :set   message  ("$message %0ACPU $cpuA");
  :set   message  ("$message %0Aarchitecture $arhA");
  :set   message  ("$message %0AROS v.$[system resource get version]");
    :if ($currFW != $upgrFW) do={set message ("$message %0A*FW is not updated*")}
    :set   message  ("$message %0AUptime $[system resource get uptime]");
    :if ($cpuZ < 90) do={:set message ("$message %0ACPU load $cpuZ%");
    } else={:set message ("$message %0A*Large CPU usage $cpuZ%*")}
    :if ($memFree > 17) do={:set message ("$message %0AMem free $memFree%");
    } else={:set message ("$message %0A*Low free mem $memFree%*")}
    :if ($hddFree > 6) do={:set message ("$message %0AHDD free $hddFree%");
    } else={:set message ("$message %0A*Low free HDD $hddFree%*")}
    :if ([:len $badBlock] > 0) do={
        :if ($badBlock = 0) do={:set message ("$message %0ABad blocks $badBlock%");
        } else={:set message ("$message %0A*Present bad blocks $badBlock%*")} }
    :if ([:len $volt] > 0) do={
        :if ($smplVolt > 4 && $smplVolt < 50) do={:set message ("$message %0AVoltage $inVolt V");
        } else={:set message ("$message %0A*Bad voltage $inVolt V*")} }
    :if ([:len $tempC] > 0) do={
        :if ($tempC > 10 && $tempC < 40) do={:set message ("$message %0ATemp $tempC C");
        } else={:set message ("$message %0A*Abnorm temp $tempC C*")} }

    :local gwList [:toarray ""];
    :local count 0;
    :local routeISP [/ip route find dst-address=0.0.0.0/0];
    :if ([:len $routeISP] > 0) do={

        # Listing all gateways
        :foreach inetGate in=$routeISP do={
            :local gwStatus [:tostr [/ip route get $inetGate gateway-status]];
            :if (([:len [:find $gwStatus "unreachable"]]=0) && ([:len [:find $gwStatus "inactive"]]=0)) do={

                # Formation of interface name
                :local ifaceISP "";
                :foreach idName in=[/interface find] do={
                    :local ifName [/interface get $idName name];
                    :if ([:len [find key=$ifName in=$gwStatus]] > 0) do={:set ifaceISP $ifName}
                }
                :if ([:len $ifaceISP] > 0) do={

                    # Checking the interface for entering the Bridge
                    :if ([:len [/interface bridge find name=$ifaceISP]] > 0) do={
#                        :local ipAddrGate [:pick $gwStatus 0 ([:find $gwStatus "reachable"] -1)];
                        :local ipAddrGate [:tostr [/ip route get $inetGate gateway]];
                        :if ([:find $ipAddrGate "%"] > 0) do={
                            :set $ipAddrGate [:pick $ipAddrGate ([:len [:pick $ipAddrGate 0 [:find $ipAddrGate "%"]] ] +1) [:len $ipAddrGate]];
                        }
                        :if ($ipAddrGate~"[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}.[0-9]{1,3}") do={
                            :local mcAddrGate [/ip arp get [find address=$ipAddrGate interface=$ifaceISP] mac-address];
                            :if ($mcAddrGate~"[0-F][0-F]:[0-F][0-F]:[0-F][0-F]:[0-F][0-F]:[0-F][0-F]:[0-F][0-F]") do={
                               :set ifaceISP [/interface bridge host get [find mac-address=$mcAddrGate] interface];
                           } else={:set ifaceISP ""}
                       } else={:set ifaceISP ""}
                    }
                    :if ([:len $ifaceISP] > 0) do={

                        # Checking the repetition of interface name
                        :if ([:len [find key=$ifaceISP in=$gwList]] = 0) do={
                            :set ($gwList->$count) $ifaceISP;
                            :set count ($count+1);
                            :local rxByte [/interface get $ifaceISP rx-byte];
                            :local txByte [/interface get $ifaceISP tx-byte];
                            :local simpleGbRxReport ($rxByte/1073741824);
                            :local simpleGbTxReport ($txByte/1073741824);
                            :local lowGbRxReport ((($rxByte-($simpleGbRxReport*1073741824))*1000000000)/1048576);
                            :local lowGbTxReport ((($txByte-($simpleGbTxReport*1073741824))*1000000000)/1048576);
                            :local gbRxReport ("$simpleGbRxReport.$[:pick $lowGbRxReport 0 2]");
                            :local gbTxReport ("$simpleGbTxReport.$[:pick $lowGbTxReport 0 2]");
                            :set message ("$message %0ATraffic via '$ifaceISP' Rx/Tx $gbRxReport/$gbTxReport Gb");
                        }
                    }
                }
            }
        }
    } else={:set message ("$message %0AWAN iface not found")}

    :if ([/ppp active find]) do={
   :foreach i in=[/ppp active find] do={
   :set $name [/ppp active get $i name]; :set $type [/ppp active get $i service]; :set $enc [/ppp active get $i encoding]; :set $addr [/ppp active get $i address]; :set $ltu [/ppp active get $i uptime]
   :set $vpnuser ("$vpnuser"."$name"." {"."$type"."}"." $addr"." uptime: "."$ltu"."%0A");}
   :set $message ("$message"."%0A"."$vpnuser")
   } else={:set message ("$message %0ANo active VPN-channels")}
   [$FuncTelegramSender $message]
   :return []
} on-error={:log warning ("Error, can't show health status"); :return "ERROR"}
 }
}

# report router`s to Telegram
# ---------------------
:set report do={
:if ([:len $0]!=0) do={
:global ADMINMAIL
:global mailsend
:global ADMINPHONE
:global smssend

:global Emoji
:global FuncTelegramSender
:if (([:len $1]=0) or ($1="tlgrm")) do={
:do {
[$FuncTelegramSender ("$Emoji"." Router "."$[/system identity get name]"." ONLINE")]
} on-error={:return "ERROR tlgrm send"}
:return "tlgrm";}
:if (($1="mail") or ($1="email")) do={
:do {
[$mailsend Email=$ADMINMAIL Mailtext=("Router "."$[/system identity get name]"." ONLINE")]
} on-error={:return "ERROR email send"}
:return $1}
:if ($1="sms") do={
:do {
[$smssend ("Router "."$[/system identity get name]"." ONLINE") $ADMINPHONE]
# /system routerboard usb power-reset duration=2s;
} on-error={:return "ERROR sms send"}
:return $1}
:if ($1="all") do={
:do {
[$FuncTelegramSender ("$Emoji"." Router "."$[/system identity get name]"." ONLINE")]
[$mailsend Email=$ADMINMAIL Mailtext=("Router "."$[/system identity get name]"." ONLINE")]
[$smssend ("Router "."$[/system identity get name]"." ONLINE") $ADMINPHONE]
} on-error={:return "ERROR $0 all send"}
:return $1}
 }
}

# lease tabl - > Telegram
# -----------------------------------
:set lease do={
:if ([:len $0]!=0) do={
:local count 0
:local txt
/ip dhcp-server lease
:if [/ip dhcp-server lease find] do={
:foreach i in=[find] do={
  :set count ($count+1)
  :local add [get $i address]
  :local mac [get $i mac-address]
  :local host [get $i host]
  :local com [get $i comment]
  :local serv [get $i server]
  :local bond [get $i status]
  :local active [get $i disabled]
  :local TT; :local WS
:if ($active) do={:set WS "enable"; :set TT "%F0%9F%94%B4"} else={:set WS "disable"; :set TT "%F0%9F%94%B5"}
# :log warning ("$count  "."$host   "."$serv "."   $add  "."$com"."  $mac "." $bond")
:set txt ("$txt"."$count  "."$TT "."$com  "."$add  "."  $mac"."%0A%20%20%20%20"."host: $host "." server: $serv"."  status: $bond"."%0A")
} 
} else={:set txt "       empty"}
:global Emoji
:global FuncTelegramSender
[$FuncTelegramSender ("$Emoji "."$[/system identity get name]"." DHCP lease tabl:"."%0A"."$txt")]
 }
}


# DHCP-clients -> Telegram
# ---------------------------------------
:set dhcpclient do={
:if ([:len $0]!=0) do={
:local count
:local output
:global Emoji
:global FuncTelegramSender

:foreach i in=[ip dhcp-client find] do={
:local cuStatus [ip dhcp-client get $i status]
:local cuComment [ip dhcp-client get $i comment]
:local cuIP [ip dhcp-client get $i address]
:local cuGW [ip dhcp-client get $i gateway]
:local cuFace [ip dhcp-client get $i interface]
:local cuAddDroute [ip dhcp-client get $i add-default-route]
:local cuDistance [ip dhcp-client get $i "default-route-distance"]
:set count ($count+1)

:set output ("$output"."$count "."$cuComment "."$cuFace "."IP: "."$cuIP "."getway:"." $cuGW "."distance"." $cuDistance "."$cuStatus"."%0A")

   }
[$FuncTelegramSender ("$Emoji "."Router "."$[/system identity get name] "."DHCP-clients table:"."%0A"."$output")]
:return $count
 }
}


#  wifi connect table -> Telegram 
# ------------------------------------------------------------
:set wificonnect do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local identity [/system identity get name]
:local count
:local output;
:foreach activeIndex in=[/interface wireless connect find true] do={
      :set count ($count+1);
              :local ConVal [/interface wireless connect get $activeIndex]
                :local iFace ($ConVal->"interface")
                :local MACAddr ($ConVal->"mac-address")
                :local comment ($ConVal->"comment")
                :local connect ($ConVal->"connect")
                :local area ($ConVal->"area-prefix")
                :local signal ($ConVal->"signal-range")
                :local SSID ($ConVal->"ssid")
                :local WP ($ConVal->"wireless-protocol")
     :if ($ConVal->"disabled") do={
             :set output ("$output"."$count"." %F0%9F%94%B4 "." $comment"." $iFace"." $MACAddr"."%0A");
} else={ :set output ("$output"."$count"." %F0%9F%94%B7  "." $comment"." $iFace"." $MACAddr"." $connect"." $area"." $signal"." $SSID"." $WP"."%0A");}

#      :log warning ("$count  "." $comment"." $iFace"." $MACAddr"." $connect"." $area"." $signal"." $SSID"." $WP"."\n")
          }

if ([:len $output] >0) do={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." wireless connect-tabl:*"."%0A"."-------------------------------------------------------------------------------------------------------------- "."%0A"."$output") "markdown"]
} else={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." wireless connect-tabl is empty*") "markdown"]}
   :return $count;
 }
}

#  Router`s user table -> Telegram 
# ------------------------------------------------------------
:set users do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local identity [/system identity get name]
:local count
:local output
:local output1
:foreach i in=[/user find true] do={
      :set count ($count+1);
                :local UserVal [/user get $i]
                :local Uname ($UserVal->"name")
                :local Ugroup ($UserVal->"group")
                :local comment ($UserVal->"comment")
                :local Ulogget ($UserVal->"last-logget-in")

     :if ($UserVal->"disabled") do={
             :set output ("$output"."$count"." %F0%9F%94%B4 "."$comment "."$Uname "."$Ugroup"." $time"."%0A");
} else={ :set output ("$output"."$count"." %F0%9F%94%B7  "."$comment "."$Uname "."$Ugroup"." $time"."%0A");}

          }

:set count
foreach i in [/user active find true] do={
      :set count ($count+1);
              :local UserVal [/user active get $i]
                :local Uname ($UserVal->"name")
                :local Via ($UserVal->"via")
  :set output1 ("$output1"."$count"." $Uname "."$Via"."%0A");

}

if ([:len $output] >0) do={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"."  users:*"."%0A"."-------------------------------------------------------------------------------------------------------------- "."%0A"."$output") "markdown"]
}
if ([:len $output1] >0) do={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." active users:*"."%0A"."-------------------------------------------------------------------------------------------------------------- "."%0A"."$output1") "markdown"]
} else={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." no active users*") "markdown"]}
  :return $count;
 }
}


# Routers modem info -> Telegram

:set modeminfo do={
:if ([:len $0]!=0) do={

:global Emoji
:global FuncTelegramSender

# lte-modems
:local lteinfo
:local LteModem [:toarray ""]

:local pinstatus
:local registrationstatus 
:local functionality
:local manufacturer 
:local model 
:local revision 
:local currentoperator 
:local lac 
:local currentcellid 
:local enbid 
:local sectorid 
:local phycellid
:local accesstechnology
:local sessionuptime
:local imei 
:local imsi 
:local uicc
:local earfcn
:local rsrp 
:local rsrq
:local sinr

:if ([/interface lte find]) do={
:foreach i in=[/interface lte find] do={
:local lteiFace [/interface lte get $i name]
:local lteComment [/interface lte get $i comment]
if ([/interface lte get $i value-name=disabled] = false) do={

:set LteModem [/interface lte monitor [find name=$lteiFace] once as-value]

:set pinstatus ($LteModem->"pin-status");
:set registrationstatus ($LteModem->"registration-status");
:set functionality ($LteModem->"functionality");
:set manufacturer ($LteModem->"manufacturer");
:set model ($LteModem->"model");
:set revision ($LteModem->"revision");
:set currentoperator ($LteModem->"current-operator");
:set lac ($LteModem->"lac");
:set currentcellid ($LteModem->"current-cellid");
:set enbid ($LteModem->"enb-id");
:set sectorid ($LteModem->"sector-id");
:set phycellid ($LteModem->"phy-cellid");
:set accesstechnology ($LteModem->"access-technology");
:set sessionuptime ($LteModem->"session-uptime");
:set imei ($LteModem->"imei");
:set imsi ($LteModem->"imsi");
:set uicc ($LteModem->"uicc");
:set earfcn ($LteModem->"earfcn");
:set rsrp ($LteModem->"rsrp");
:set rsrq ($LteModem->"rsrq");
:set sinr ($LteModem->"sinr");

:set lteinfo ("%E2%9C%8C"." LTE-modem "."$lteiFace  info:"."%0A"."$pinstatus"."%0A"."$registrationstatus"."%0A"."$functionality"."%0A"."$manufacturer"."%0A"."$model"."%0A"."$revision"."%0A"."$currentoperator"."%0A"."$lac"."%0A"."$currentcellid"."%0A"."$enbid"."%0A"."$sectorid"."%0A"."$phycellid"."%0A"."$accesstechnology"."%0A"."$sessionuptime"."%0A"."$imei"."%0A"."$imsi"."%0A"."$uicc"."%0A"."$earfcn"."%0A"."$rsrp"."%0A"."$rsrq"."%0A"."$sinr")

[$FuncTelegramSender ("$Emoji "."$[/system identity get name]"." $lteinfo")]

  } else={[$FuncTelegramSender ("$Emoji "."$[/system identity get name] "."LTE-interface "."$lteiFace "."$lteComment "."disabled")]}
 }
} else={[$FuncTelegramSender ("$Emoji "."$[/system identity get name] "."no found lte modem")]}

# modems ppp-client
:local nameFind [:toarray ""]
:local calc 0
:foreach i in=[/interface ppp-client find] do={
:set  calc ($calc+1)
if ([/interface ppp-client get $i value-name=disabled] = false) do={
:local tmp [/interface ppp-client info $i once as-value]
:set $nameFind ($nameFind, {{"name"=[/interface ppp-client get $i value-name=name]; "comment"=[/interface ppp-client get $i comment]; "type"="ppp-client";"manufacturer"=($tmp->"manufacturer");"model"=($tmp->"model");"revision"=($tmp->"revision")}})
  } else={[$FuncTelegramSender ("$Emoji "."$[/system identity get name] "."ppp-client modem "."$[/interface ppp-client get $i name] "."$[/interface ppp-client get $i comment] "."disabled")]}
}

:if ([:len $nameFind]!= 0) do={
:local mName
:local mType
:local mComment
:for i from=0 to ([:len $nameFind]-1) do={
:set mName ($nameFind->$i->"name")
:set mComment ($nameFind->$i->"comment")
:set mType ($nameFind->$i->"type")
[$FuncTelegramSender ("$Emoji "."$[/system identity get name] "."$mComment "."$mName "."$mType")]
}
} else={
:if ($calc=0) do={
[$FuncTelegramSender ("$Emoji "."$[/system identity get name] "."no found ppp-client modem")]}}
:return []
 }
}


# - Log in-> Telegram

:set log do={
:if ([:len $0]!=0) do={

:global Emoji
:global botID
:global myChatID
:global FuncTelegramSender

:local name [/system identity get name]
:local LogsAll [/log print count-only]
:local counter 0
:local log1
:local otstup $2
:local logs $1

:if ($otstup ~ "[a-z|A-Z|_-|+/|*?]") do={:set otstup 0}
:foreach i in=[/log find] do={
  :if (($counter >= ($LogsAll - $otstup - $logs)) and ($counter < ($LogsAll - $otstup))) do={
  :local Log1Time [/log get $i time]
  :local Log1Message [/log get $i message]
    :set log1 ($log1."|".$Log1Time." ".$Log1Message."%0A")
  }
  :set counter ($counter + 1)
}
:set otstup 0
[$FuncTelegramSender ("$Emoji "."$name: "."$logs line logs:"."%0A"."$log1")]
  :return $counter
 }
}


# - Reset Logging and setup length log ($1)
:set logreset do={
if ([:len $0]!=0) do={
:local LineLog 
:if ([:len $1]=0) do={:set LineLog 1000} else={:set LineLog $1}
/system logging action set memory memory-lines=1;
/system logging action set memory memory-lines=$LineLog;
:log warning "Logging is reset. A log of $LineLog entries is set"
:return $LineLog
 }
}

# - pingpong 15/04/2022 Функция проверки доступности устройства в сети
#   c оповещением в Телеграмм. Использует FuncPing

:set pingpong do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:global FuncPing
:local PA; :local PC; :local PI; :local PRT; :local Hadr;
:if ([:len $1]!=0) do={:set PA $1} else={:set PA 8.8.8.8}
:if ([:len $2]!=0) do={:set PC $2} else={:set PC [:tonum "3"]}
:if ([:len $3]!=0) do={:set PI $3} else={:set PI ""}
:if ([:len $4]!=0) do={:set PRT $4} else={:set PRT "main"}
:local PingAns [$FuncPing PingAdr=$1 PingCount=$2 PingInterface=$3 PingRoutingTabl=$4]
:if ($PingAns="ERROR") do={[$FuncTelegramSender ("$Emoji "."Host "."$PA "."not responded")]; :return "ERROR"}
:if ($PingAns="OK") do={[$FuncTelegramSender ("$Emoji "."Host "."$PA "."ping OK")]; :return "OK"}
 }
}

:global FuncActIface do={
# Get address active gateway - in var $activeGateway
/ip route
 {   :local counter;
     :foreach counter in=[find dst-address=0.0.0.0/0] do={         
	 :if ([get $counter active] = true) do={
	 :set $activeGateway [get $counter gateway]; :set $Gdistance [get $counter distance];
	 }  }}
:if ($activeGateway!=nil) do={

# Get Gateway with gateway-status active
:local GatewayStatus;
:set $GatewayStatus [ :tostr [ /ip route get [ find gateway=$activeGateway dst-address=0.0.0.0/0 distance=$Gdistance] gateway-status ]];


:if ([:find $GatewayStatus "via"] > 0) do={
# Seek Interface name in $GatewayStatus (after "via" verb)
:local activeInterface;
:set $activeInterface [ :pick $GatewayStatus ( [ :len [ :pick $GatewayStatus 0 [ :find $GatewayStatus "via" ] ] ] + 5 ) [ :len $GatewayStatus ] ]; :return $activeInterface
} else={ 
:set $activeInterface [ :pick $GatewayStatus 0 ([:find $GatewayStatus "reachable"]-1)]; :return $activeInterface;}

} else={:local activeInterface "not found"; :return $activeInterface;}
}



# - FuncPing 15/03/2022 Функция проверки доступности устройства в сети
#  возможные именованные параметры:
# PingAdr -     адрес пингуемого хоста, если не указан пингуется 8.8.8.8 (доступность Интернета)
#                    допускается передача PingAdr с указанием порта (отсекается)
# PingCount - число пингов, ести не указано, по умолчанию устанавливается 3 пинга
# PingIntherface - интерфейс для пинга, может быть не указан, тогда используется default gatway
# PingRoutingTabl - таблица маршрутов (если не указана используется таблица main

# все парамметры пинга могут быть переданы ТОЛЬКО ! в именованных параметрах
# :put [$FuncPing PingAdr=8.8.8.8 PingCount=3 PingInterface=ether1 PingRoutingTabl=main]


# ответ функции возвращается в виде:
# "OK" - устройство доступно в сети
# "ERROR" - устройство не отвечает на пинг

:set FuncPing do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local PA; :local PC; :local PI; :local PRT; :local Hadr;

:if ([:len $PingAdr]!=0) do={:set PA $PingAdr} else={:set PA 8.8.8.8}
:set Hadr $PA
:if ([:len $PingCount]!=0) do={:set PC $PingCount} else={:set PC [:tonum "3"]}
:local PingCalc $PC
:if ([:len $PingInterface]!=0) do={:set PI $PingInterface} else={:set PI ""}
:local PingIface
:if ($PI="") do={:set PingIface "";} else {:set PingIface ("interface="."$PI")}
:if ([:len $PingRoutingTabl]!=0) do={:set PRT $PingRoutingTabl} else={:set PRT "main"}
:local PingRT ("routing-table="."$PRT")
:if ([:find $Hadr ":"]>0) do={:set Hadr [:pick $Hadr 0 [:find $Hadr ":"]];}
:local Result [[:parse "[/ping $Hadr count=$PingCalc $PingIface $PingRT]"]]
:beep frequency=300 length=494ms; :delay 70ms; :beep frequency=600 length=494ms; :delay 70ms; :beep frequency=900 length=494ms;
:local PingAnswer ""; :local MainIfInetOk false;
:set MainIfInetOk ((3*$Result) >= (2 * $PingCalc))
if (!$MainIfInetOk) do={:return "ERROR"} else={:return "OK"}
 }
}


# - mailsend - функция отправки почты 
#    by Sergej Serkov 25/10/2022
#-------------------------------------------------------

# для корректной работы функции почтовый сервис Router OS /tool email должен быть настроен верно по умолчанию
# адрес получателя и текст письма могут быть переданы в именованных либо позиционных параметрах (или/или)
# в случае с позиционными параметрами $1 - адрес получателя; $2 - текст сообщения (местами менять нельзя)
# при передаче адреса и текста письма в именованных параметрах (Email и Mailtext) - их порядок не важен

# for example:
# :log info [$mailsend Email="user@mail.ru" Mailtext="Привет !"]; # (с именованными параметрами) 
# :log info [$mailsend user@mail.ru "Привет !"]; # (c позиционными параметрами)

:set mailsend do={
:if ([:len $0]!=0) do={ 
:local Etls "yes"; # если TLS не используется установить в "no"
:local smtpserv;
do {
:set smtpserv [:resolve [/tool e-mail get address]];
} on-error={:log error ("Call ERROR function $0 not resolve email smtp server"); :return ("ERROR: $0 < not resolve email smtp server >")}
:local Eaccount [/tool e-mail get user];
:local pass [/tool e-mail get password];
:local Eport [/tool e-mail get port];
:local MA; :local MT
:if ([:len $1]!=0) do={:set MA $1} else={:set MA $Email}
:if ([:len $2]!=0) do={:set MT $2} else={:set MT $Mailtext}
:if ((any $MA) and (any $MT)) do={
:log info " "; :log warning "FuncMail start mail sending ... to e-mail: $Email";
do {[/tool e-mail send from="<$Eaccount>" to=$MA server=$smtpserv \
 port=$Eport user=$Eaccount password=$pass tls=starttls subject=("from $0 Router $[/system identity get name]") \
 body=$MT;];
              } on-error={:log info ""; :log error ("Call ERROR function $0 ERROR e-mail send"); 
                                                                                      :return "ERROR: <$0 e-mail send>"}
:log warning "Mail send"; :log info " "; :return "OK: <mail send>"
} else={:log error ("Call ERROR function $0 Email or Mailtext parametrs no defined"); :return ("ERROR: $0 < necessary parameters are not set >")}
 }
}


# smssend 
# --------------

# [$smssend "Hello Привет мир" +79104797777]

# local function transliteration
# string for transliteration is set in the parametr name "string"

:set smssend do={
:local FuncSatTranslite do={
#  table of the codes of Russian letters Translite
:local rsimv [:toarray {"А"="A"; "Б"="B"; "В"="V"; "Г"="G"; "Д"="D"; "Е"="E"; "Ж"="ZH"; "З"="Z"; "И"="I"; "Й"="J"; "К"="K"; "Л"="L"; "М"="M"; "Н"="N"; "О"="O"; "П"="P"; "Р"="R"; "С"="S"; "Т"="T"; "У"="U"; "Ф"="F"; "Х"="KH"; "Ц"="C"; "Ч"="CH"; "Ш"="SH"; "Щ"="SCH"; "Ъ"="``"; "Ы"="Y`"; "Ь"="`"; "Э"="E`"; "Ю"="JU"; "Я"="YA"; "а"="a"; "б"="b"; "в"="v"; "г"="g"; "д"="d"; "е"="e"; "ж"="zh"; "з"="z"; "и"="i"; "й"="j"; "к"="k"; "л"="l"; "м"="m"; "н"="n"; "о"="o"; "п"="p"; "р"="r"; "с"="s"; "т"="t"; "у"="u"; "ф"="f"; "х"="kh"; "ц"="c"; "ч"="ch"; "ш"="sh"; "щ"="sch"; "ъ"="``"; "ы"="y`"; "ь"="`"; "э"="e`"; "ю"="ju"; "я"="ya"; "Ё"="Yo"; "ё"="yo"; "№"="#"}]

# encoding of the symbols and аssembly line
:local StrTele ""; :local code "";
:for i from=0 to=([:len $string]-1) do={:local keys [:pick $string $i (1+$i)];

:local key ($rsimv->$keys); if ([:len $key]!=0) do={:set $code ($rsimv->$keys);} else={:set $code $keys};

:if (($keys="Ь")  and ([:pick $string ($i+1) (2+$i)]="Е")) do={:set $code "I"; :set $i ($i+1)}
:if (($keys="ь")  and ([:pick $string ($i+1) (2+$i)]="е")) do={:set $code "i"; :set $i ($i+1)}
:if (($keys="Ь")  and ([:pick $string ($i+1) (2+$i)]="е")) do={:set $code "I"; :set $i ($i+1)}
:if (($keys="ь")  and ([:pick $string ($i+1) (2+$i)]="Е")) do={:set $code "i"; :set $i ($i+1)}
:if (($keys="Ы")  and ([:pick $string ($i+1) (2+$i)]="Й")) do={:set $code "I"; :set $i ($i+1)}
:if (($keys="ы")  and ([:pick $string ($i+1) (2+$i)]="й")) do={:set $code "i"; :set $i ($i+1)}
:if (($keys="ы")  and ([:pick $string ($i+1) (2+$i)]="Й")) do={:set $code "i"; :set $i ($i+1)}
:if (($keys="Ы")  and ([:pick $string ($i+1) (2+$i)]="й")) do={:set $code "I"; :set $i ($i+1)}
 :set $StrTele ("$StrTele"."$code")}
:return $StrTele
}

         :if ([:len $0]!=0) do={
         :local SMSdevice [/tool sms get port];
         :local NumPhone
         :global ADMINPHONE;
         :if ([:len $2]!=0) do={:set NumPhone $2} \
              else={
                     :if ([:len $ADMINPHONE]!=0) do={:set NumPhone $ADMINPHONE} \
                            else={
                                :local NumSMS [/tool sms get allowed-number];
                                     :if ([:len $NumSMS]!=0) do={:set NumPhone ($NumSMS->0)} \
                                             else={:log error "ERROR $0 sms phone number not found"; :return "ERROR function $0 sms phone number"}
}}

# must be performed translite
:set $1 [$FuncSatTranslite string=$1]

         :log info ""; :log warning "Function $0 start sms sending to $NumPhone";
             :do {
                [/tool sms send  $SMSdevice phone=$NumPhone message=$1];
                } on-error={:log error "ERROR $0 sms send"; :return "ERROR sms"}
          :log warning "Function $0 sms sent via modem"; :log info "";
          :return "done sms"
    } else={:log error "ERROR $0 sms send but no message text"; :return "ERROR sms" }
}


# function satclear 
# ------------------------------------------------------------
:set satclear do={
:if ([:len $0]!=0) do={

:global satlogo; :set satlogo
:global satlist; :set satlist 
:global arp; :set arp
:global address; :set address
:global backup; :set backup
:global lease; :set lease
:global report; :set report
:global status; :set status
:global vpn; :set vpn
:global vpnuser; :set vpnuser
:global wifi; :set wifi
:global wifireg; :set wifireg
:global wifiaccess; :set wifiaccess
:global wificonnect; :set wificonnect
:global wifipass; :set wifipass
:global health; :set health
:global dhcpclient; :set dhcpclient
:global users; :set users;
:global scriptlist; :set scriptlist
:global SchedList; :set SchedList
:global funclist; :set funclist
:global log; :set log
:global logreset; :set logreset
:global pingpong; :set pingpong
:global mailsend; :set mailsend
:global smssend; :set smssend
:global modeminfo; :set modeminfo
:global globalvarlist; :set globalvarlist
   
  :if ($1="all") do={
:global satlist; :set satlist 
:global FuncPing; :set FuncPing; 
:global FuncSchedFuncAdd; :set FuncSchedFuncAdd;
:global FuncSchedScriptAdd; :set FuncSchedScriptAdd;
:global FuncSchedRemove; :set FuncSchedRemove;
:global FuncUnixTimeToFormat; :set FuncUnixTimeToFormat;
:global FuncEpochTime; :set FuncEpochTime;
:global FuncTelegramSender; :set FuncTelegramSender;
}
:global satclear; :set satclear
:global tlgrmcmd; :if (any $tlgrmcmd) do={:local arCom [:toarray {"satstart"="установка библиотеки SATELLITE в окружение"}];
:log warning ("list of commands of the SATELLITE library in the Telegram chatbot erase is $[$tlgrmcmd $arCom]")}
        :put  ("$[/system identity get name]"." $0 unload")
        :return ("library $0 "." is unload")
 }
}

:log info "  -    SATELLITE2 module is set"
