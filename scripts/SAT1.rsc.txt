#--------------------------------------------------------------------------------------------------------------------------
# SATELLITE1 module  for TLGRM version 2.2 by Sertik (Serkov S.V.) 25/10/2022
#--------------------------------------------------------------------------------------------------------------------------

# declare functions:

:global arp
:global address
:global backup
:global status
:global vpn 
:global vpnuser
:global wifi 
:global wifireg 
:global wifiaccess
:global wifipass
:global scriptlist
:global funclist
:global schedlist
:global globalvarlist


#
#   КОМАНДЫ БИБЛИОТЕКИ SATELLITE
#

# arp tabl - > Telegram
# ---
:set arp do={
:if ([:len $0]!=0) do={
:local TXTarp
:global Emoji
:global FuncTelegramSender
:local count 0
:local WS
:local TT
foreach i in=[ /ip arp find] do={
:local active [/ip arp get $i disabled]
:if ($active) do={:set WS "enable"; :set TT "%F0%9F%94%B4"} else={:set WS "disable"; :set TT "%F0%9F%94%B5"}
:local ipARPaddress [/ip arp get $i address];
:local ipARPmacaddress [/ip arp get $i mac];
:local ARPface [/ip arp get $i interface];
:local ARPcomment [/ip arp get $i comment];
:if ([:len $ipARPmacaddress]!=0) do={:set count ($count+1); :set $TXTarp ("$TXTarp"."$count "."$TT "."$ipARPaddress "." $ipARPmacaddress "." $ARPface "." $ARPcomment"."%0A")
  }
}

[$FuncTelegramSender ("$Emoji "." $[/system identity get name]"." arp tabl:"."%0A"."$TXTarp")]
:return $count
 }
}

# ip addresses tabl - > Telegram
# ----
:set address do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local system [/system identity get name]
:local WS
:local listAL
:local TT
:local count 0

:foreach AddrList in=[/ip address find true] do={
:local Aname [/ip address get $AddrList]
:local Aadr ($Aname->"address")
:local Acomment ($Aname->"comment")
:local Anetwork ($Aname->"network")
:local Aface ($Aname->"actual-interface")
:local WsState [/ip address get $AddrList disabled]
:if ($WsState) do={:set WS "enable"; :set TT "%F0%9F%94%B4"} else={:set WS "disable"; :set TT "%F0%9F%94%B5"}
:set count ($count+1)
:set listAL ("$listAL"."$count"." $TT"." $Aadr"." $Acomment"." $Anetwork"." $Aface"."%0A")
}
:if ([:len $listAL]!=0) do={
[$FuncTelegramSender ("$Emoji "." $system"." /ip address :%0A"."$listAL")]
   }
 :return $count
 }
}


# backup  - > Telegram
# ---
:set backup do={
:if ([:len $0]!=0) do={
# send or no send on E-mail
:local mailsend false
:global ADMINMAIL;  
:local mailBox $ADMINMAIL;  

:log warning "Starting Backup Script...";
:local sysname [/system identity get name];
:local sysver [/system package get system version];
:log info "Flushing DNS cache...";
/ip dns cache flush;
:delay 2;
:log info "Deleting last Backups...";
:foreach i in=[/file find] do={:if (([:typeof [:find [/file get $i name] \
"$sysname-backup-"]]!="nil") or ([:typeof [:find [/file get $i name] \
"$sysname-script-"]]!="nil")) do={/file remove $i}};
:delay 2;
:local backupfile ("$sysname-backup-" . \
[:pick [/system clock get date] 7 11] . [:pick [/system \
clock get date] 0 3] . [:pick [/system clock get date] 4 6] . ".backup");
:log warning "Creating new Full Backup file...$backupfile";
/system backup save name=$backupfile;
:delay 5;
:local exportfile ("$sysname-backup-" . \
[:pick [/system clock get date] 7 11] . [:pick [/system \
clock get date] 0 3] . [:pick [/system clock get date] 4 6] . ".rsc");
:log warning "Creating new Setup Script file...$exportfile";
/export verbose file=$exportfile;
:delay 5;
:local scriptfile ("$sysname-script-" . \
[:pick [/system clock get date] 7 11] . [:pick [/system \
clock get date] 0 3] . [:pick [/system clock get date] 4 6] . ".rsc");
:log warning "Creating new file export all scripts ...$scriptfile";
/system script export file=$scriptfile;
:delay 2;
:log warning "All System Backups and export all Scripts created successfully.\nBackuping completed.";

if ($mailsend) do={
:log warning "Sending Setup Script file via E-mail...";
:local smtpserv [:resolve "smtp.mail.ru"];
:local Eaccount [/tool e-mail get user];
:local pass [/tool e-mail get password];
/tool e-mail send from="<$Eaccount>" to=$mailBox server=$smtpserv \
port=587 user=$Eaccount password=$pass tls=starttls file=($backupfile, $exportfile, $scriptfile) \
subject=("$sysname Setup Script Backup (" . [/system clock get date] . \
")") body=("$sysname Setup Script file see in attachment.\nRouterOS \
version: $sysver\nTime and Date stamp: " . [/system clock get time] . " \
" . [/system clock get date]);
:log warning "Setup Script file e-mail send";
:delay 5;
  }
:return "backup is done"
 }
}


# all resurces - > Telegram
# ---
:set status do={
  :if ([:len $0]!=0) do={
    :global Emoji;
    :global FuncTelegramSender
    :local systemName       [/system identity get name];
    :local uptime           [/system resource get uptime];
    :local FreeMemory       [/system resource get free-memory];
    :local TotalMemory      [/system resource get total-memory];
    :local cpu              [/system resource get cpu];
    :local cpuCount         [/system resource get cpu-count];
    :local cpuFrequency     [/system resource get cpu-frequency];
    :local cpuLoad          [/system resource get cpu-load];
    :local freeHdd          [/system resource get free-hdd-space];
    :local totalHdd         [/system resource get total-hdd-space];
    :local architectureName [/system resource get arch]
#  :local license          [/system license get level];
    :local boardName        [/system resource get board-name];
    :local version          [/system resource get version];

:local TXT ("$Emoji "."$systemName"." status:"."%0A"."Uptime: "."$uptime"."%0A"."Free Memory: "."$FreeMemory"." B"."%0A"."Total Memory: "."$TotalMemory"." B"."%0A"."CPU ". "$cpu"."%0A"."CPU Count: "."$cpuCount"."%0A"."CPU Frequency: "."$cpuFrequency"."MHz"."%0A"."CPU Load: "."$cpuLoad"."% "."%0A"."Free HDD Space "."$freeHdd"." B "."%0A"."Total HDD Space: "."$totalHdd"." B"."%0A"."Architecture: "."$architectureName "."%0A"."License Level: "."$license"."%0A"."Board Name:  "."$boardName"." %0A"."Version: ". "$version")
:do {
[$FuncTelegramSender $TXT]
} on-error={}
:return [];
 }
}


# function VPN-tonnel scanning & Telegram chat send

:set vpn do={
:if ([:len $0]!=0) do={
# used: [$FuncTonnel "tonnelType"]
:local FuncTonnel do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local system [/system identity get name]
:local count 0
:local Cto
:local listVpn
:foreach Vpn in=[[:parse "[/interface $1 find]"]] do={
  :local TT "%F0%9F%94%B5"
  :local nameVpn [[:parse "[/interface $1 get $Vpn name]"]]
  :local commentVpn [[:parse "[/interface $1 get $Vpn comment]"]]
  :local typeVpn [:pick [/interface get $nameVpn type] ([:find [/interface get $nameVpn type] "-"]+1) [:len [/interface get $nameVpn type]]]
  :if ($typeVpn="out") do={:set Cto ("connect to "."$[[:parse "[/interface $1 get $nameVpn connect-to]"]]")} else={:set Cto ""}
  :local VpnState [[:parse "[/interface $1 monitor $Vpn once as-value]"]]
  :local cuVpnStatus ($VpnState->"status")
  :local ladr ($VpnState->"local-address")
  :local radr ($VpnState->"remote-address")
  :local uptime ($VpnState->"uptime")

    :if ($cuVpnStatus~"terminating") do={
    :set cuVpnStatus "disabled";  set TT ""}
    :if ([:typeof $cuVpnStatus]="nothing") do={
    :set cuVpnStatus "disconnected";  set TT "%F0%9F%94%B4"}
    :if ($cuVpnStatus="disabled") do={ :set TT "%F0%9F%94%B3"}
  :set count ($count+1)
 :if (($cuVpnStatus="disconnected") or ($cuVpnStatus="disabled")) do={
  :set listVpn ($listVpn."".$count. " ".$TT." ".$nameVpn." ".$commentVpn.": ".$cuVpnStatus."%0A")
} else={:set listVpn ("$listVpn"."$count"." $TT"." $nameVpn"." $commentVpn:"." $cuVpnStatus"." uptime: "."$uptime"."%0A"."          $Cto"." local-address: "."$ladr"." remote-address: "."$radr"."%0A")}
}
:if ([:len $listVpn]!=0) do={:global xcount; :set xcount ($xcount+1)
[$FuncTelegramSender ("$Emoji "." $system"." $1:%0A"."$listVpn")]
 }
}
:return $count}


# main block of the script
:global Emoji
:global FuncTelegramSender
[$FuncTelegramSender ("<b>"."$Emoji "." $system"." --- VPN Interface Informer --- : </b> %0A %0A") "html"]
:log info ""
:log warning ("$system"."VPN Interface Informer script is start up ... ")
:global xcount 0
[$FuncTonnel "l2tp-client"]
[$FuncTonnel "l2tp-server"]
[$FuncTonnel "pptp-client"]
[$FuncTonnel "pptp-server"]
[$FuncTonnel "ovpn-client"]
[$FuncTonnel "ovpn-server"]
[$FuncTonnel "sstp-client"]
[$FuncTonnel "sstp-server"]
:if ($xcount=0) do={[$FuncTelegramSender ("<b>"."$Emoji "." $system"." --- No find PPP VPN Interface  --- : </b> %0A %0A") "html"]}
# [$FuncTonnel "ppp-client"]
# [$FuncTonnel "ppp-server"]
# [$FuncTonnel "pppoe-client"]
# [$FuncTonnel "pppoe-server"]
:set xcount

:log warning "VPN Interface Informer scanning is done and Telegram chat send"
:return "done"
}}


# print vpnuser seckret & active vpn user`s - > Telegram
# версия 01.10.2021
# ---
:set vpnuser do={
:if ([:len $0]!=0) do={
:local name;
:local enc;
:local addr;
:local ltu;
:local pass;
:local type;
:local vpnuser
:local vpnuserT
:local vpnuserL
:local vpnuserTlg
:local calc
:global ADMINPHONE
:global Emoji


#:log warning "Настроенные VPN-пользователи:"

:foreach i in=[/ppp secret find] do={
:set name [/ppp secret get $i name]; :set pass [/ppp secret get $name password]; set type [/ppp secret get $i service]
:set calc ($calc+1); 
# :log info ("$calc "."$name"." ["." $type"."]"." $pass");
:set vpnuser ("$vpnuser"."$calc"."  $name"."  <"." $type ".">"." $pass"."%0A");
}

# :log warning $calc; :log info "";
:do {
:global FuncTelegramSender; [$FuncTelegramSender ("$Emoji "."* $[/system identity get name]*"." VPN-users name, type and password set:"."%0A------------------------------------------------------------------------------------%0A"."$vpnuser") "markdown"]
} on-error={:log error "Error send to Telegram message"}

:set vpnuser;
:set calc

#:log warning "Подключенные VPN-пользователи:"

:foreach i in=[/ppp active find] do={
:set $name [/ppp active get $i name]; :set $type [/ppp active get $i service]; :set $enc [/ppp active get $i encoding]; :set $addr [/ppp active get $i address]; :set $ltu [/ppp active get $i uptime]
# :log info ("$name"." ["."$type"."]"." $enc"." $addr");
:set $vpnuser ("$vpnuser"."$name"."\n");
:set vpnuserTlg ("$vpnuserTlg"."$name"."  {"."$type"."}"." $addr". " uptime: $ltu"."%0A");
}

:set calc [:len [/ppp active find]]

do {
:local SMSdevice [/tool sms get port];
/tool sms send $SMSdevice  phone=$ADMINPHONE message=("active VPN-users "."$calc".":"."\n"."$vpnuser");
} on-error={:log error "Error send SMS message"}

do {
:global FuncTelegramSender;
[$FuncTelegramSender ("$Emoji "."* $[/system identity get name]*"." VPN-active". " $calc"." users: "."%0A-----------------------------------------------------------------------%0A"."$vpnuserTlg") "markdown"]
} on-error={:log error "Error send to Telegram message"}
  :return $calc
 }
}

# print wifi Interfaces, SSID and Band to Telegram
# версия 06.03.2022
# ---
:set wifi do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local system [/system identity get name]
:local WS
:local listWS
:local logWS
:local TT
:local count 0

:foreach wirelessClient in=[/interface wireless find true] do={
:local WsName [/interface get $wirelessClient name]
:local WsComment [/interface get $wirelessClient comment]
:local WsSID [/interface wireless get $wirelessClient ssid] 
:local WsBand [/interface wireless get $wirelessClient band] 
:local WsMode [/interface wireless get $wirelessClient mode] 
:local WsState (![/interface get $wirelessClient disabled])
:local WsProf  [/interface wireless get $wirelessClient security-profile]
:local Ws2Key [/interface wireless security-profiles get $WsProf wpa2-pre-shared-key];
:if ([:len $WsBand]=0) do={:set WsMode "virtual wifi"}
:if ($WsState) do={:set WS "enable"; :set TT "%F0%9F%94%B5"} else={:set WS "disable"; :set TT "%F0%9F%94%B4"}
# :local WD [/interface monitor $wirelessClient once as-value]
:set count ($count+1)
:set listWS ("$listWS"."$count"." $TT"." $WsName"." $WsComment"." [ $WsMode ]"." SSID:"." $WsSID"." $WsBand"."%0A")
:set logWS ("$logWS"."$count"." $WsSID "."$Ws2Key"."\n")
}

:if ([:len $listWS]!=0) do={
[$FuncTelegramSender ("$Emoji "." $system"." wifi interface and SSID:%0A"."$listWS")]
   }
 :return $logWS;
 }
}

# wifiaccess tabl to Telegram
# ---
:set wifiaccess do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:local identity [/system identity get name]
:local count
:local output;
:foreach activeIndex in=[/interface wireless access find true] do={
      :set count ($count+1);
              :local RegVal [/interface wireless access get $activeIndex]
                :local iFace ($RegVal->"interface")
                :local MACAddr ($RegVal->"mac-address")
                :local comment ($RegVal->"comment")
                :local time ($RegVal->"time")
     :if ($RegVal->"disabled") do={
             :set output ("$output"."$count"." %F0%9F%94%B4 "." $comment"." $iFace"." $MACAddr"." $time"."%0A");
} else={ :set output ("$output"."$count"." %F0%9F%94%B7  "." $comment"." $iFace"." $MACAddr"." $time"."%0A");}

          }

if ([:len $output] >0) do={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." wireless access-tabl:*"."%0A"."-------------------------------------------------------------------------------------------------------------- "."%0A"."$output") "markdown"]
} else={[$FuncTelegramSender ("$Emoji"." *Router"." $identity"." wireless access-tabl is empty*") "markdown"]}
  :return $count;
 }
}

# wifi users registry tabl - > Telegram
# ---
:set wifireg do={
:if ([:len $0]!=0) do={
:global Emoji;
:global botID;
:global myChatID;
:global FuncTelegramSender
:local count
:local identity [/system identity get name];

:local output;
:foreach activeIndex in=[/interface wireless registration-table find] do={
            :if ([:typeof $activeIndex]!="nothing") do={
                         :local WifiFace [/interface wireless registration-table get value-name="interface" $activeIndex];
                         :local WifiComment [/interface wireless registration-table get value-name="comment" $activeIndex];
                         :local activeMACAddr [/interface wireless registration-table get value-name="mac-address" $activeIndex];
                         :local activeIPadr [/interface wireless registration-table get value-name="last-ip" $activeIndex];
                         :local WSignal [/interface wireless registration-table get value-name="signal-strength" $activeIndex];
                         :set count ($count+1)
                         :set output ("$output"."$count"."  $WifiFace"." $activeMACAddr "."$activeIPadr"." $WifiComment"."%0A"."      signal strength: "."$WSignal"."%0A");
                  }
          }

if ([:len $output] >0) do={[$FuncTelegramSender ("$Emoji"." <b>Router  $identity wireless registration-tabl:</b>"."%0A"." $output1") "html"]
} else={[$FuncTelegramSender ("$Emoji"." <b>Router  $identity wireless registration-tabl: is empty</b>") "html"]}
:return $count;
 }
}


# wifi SSID & pass - > Telegram and SMS $ADMINPHONE
# ---
:set wifipass do={
:if ([:len $0]!=0) do={
:global Emoji
:global FuncTelegramSender
:global ADMINPHONE
:global FuncWifi
:local SMSmessage [$FuncWifi]
:log warning ("\n"."Router "."$[/system identity get name]"." wlan SSID and passwords:"."\n"."$SMSmessage")
do {
:local SMSdevice [/tool sms get port];
 /tool sms send $SMSdevice  phone=$ADMINPHONE message=("Router "."$[/system identity get name]"." wifi:"."\n"."$SMSmessage")
} on-error={:log error "Error send SMS message"}
   :return [];
 }
}

# function scriptlist ->Telegram
# ---
:set scriptlist do={
:if ([:len $0]!=0) do={
:global Emoji; :global FuncTelegramSender; :global GroupChat; :global broadCast;
:local Router [/system identity get name]
:local ScriptArray [:toarray ""]; :local CommentArray [:toarray ""]
:local nlist 900
:local name
:local comment
:local count 0
:if (([:len $1]>0) and ($1!="fastcall")) do={:return "Function $0 bad parametr $1"}

# seek all scripts or ONLY which can be launched from the chat !
:if ($1="fastcall") do={
:foreach i in=[system script find] do={
:if (([:len [:find [/system script get $i name] "_"]]=0) && ([:len [:find [/system script get $i name] " "]]=0)) do={
:set $name [/system script get $i name]; set comment [/system script get $i comment]
:set ($ScriptArray->$count) $name; :set ($CommentArray->$count) $comment;:set count ($count+1);}
 }
} else={
:foreach i in=[system script find] do={
:set $name [/system script get $i name]; set comment [/system script get $i comment]
:set ($ScriptArray->$count) $name; :set ($CommentArray->$count) $comment;:set count ($count+1);}
}

:local a ""; :local b; :local c; :local d;
:local block 0
:for i from=0 to=([:len $ScriptArray]-1) do={:set $c ("$ScriptArray"->"$i"); :set $d ("$CommentArray"->"$i");:set b ($i+1)

# учитывать флаг режима широковещательной рассылки
:if (!$broadCast) do={:set a ("$a"."$b "."/"."$Router"."_"."$c$GroupChat"." $d"."%0A")} else={:set a ("$a"."$b "."/"."$c"." $d"."%0A")}

# один вывод ограничен количеством nlist символов;
:if ([:len $a]>$nlist) do={:set block ($block+1);
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository script list < $block >:"."%0A%0A"."$a")]
:set a "";
  }
}
:set block ($block+1)
# "допечатка" последнего блока данных
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository script list: < $block >"."%0A%0A"."$a")]
  :return [:len $ScriptArray]
 }
}

# function funclist ->Telegram
# ---
:set funclist do={
:if ([:len $0]!=0) do={
:global Emoji; :global FuncTelegramSender; :global broadCast; global GroupChat;
:local Router [/system identity get name]
:local ScriptArray [:toarray ""];
:local ValArray [:toarray ""];
:local nlist 900
:local name
:local val
:local comment
:local count 0

# seek all global
:foreach i in=[/system script environment find] do={:set $name [/system script environment get $i name]; 
:if (([/system script environment get $i value]="(code") or  ([:len [:find [/system script environment get $i value] "(eval"]]>0)) do={
:set ($ScriptArray->$count) $name;
:set count ($count+1);
}}
:local a ""; :local b 0; :local c;
:local block 0
:for i from=0 to=([:len $ScriptArray]-1) do={:set $c ("$ScriptArray"->"$i");
:if ([:len $c]!=0) do={

# учитывать флаг широковещательной рассылки
:if (!$broadCast) do={:set b ($b+1); :set a ("$a"." - "."$b  "."/"."$Router"."_"."$c$GroupChat"."%0A")} else={:set b ($b+1); :set a ("$a"." - "."$b  "."/"."$c "."%0A")}

}
# один вывод ограничен количеством nlist символов;
:if ([:len $a]>$nlist) do={:set block ($block+1);
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository environment active Functions list < $block >:"."%0A%0A"."$a")]
:set a "";
  }
}
:set block ($block+1)
# "допечатка" последнего блока данных
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository environment active Functions list: < $block >"."%0A%0A"."$a")]
     :return $count;
 }
}

# function schedlist ->Telegram
# ---
:set schedlist do={
:if ([:len $0]!=0) do={
:global Emoji; :global FuncTelegramSender;
:local ScriptArray [:toarray ""]; :local CommentArray [:toarray ""]; :local StartTimeArray [:toarray ""]; :local IntervalArray [:toarray ""]; local StatusArray [:toarray ""];
:local nlist 900
:local count 0
# seek all scheduler
:foreach i in=[system scheduler find] do={
:set ($StatusArray->$count) [/system scheduler get $i disabled]
:set ($ScriptArray->$count) [/system scheduler get $i name]
:set ($CommentArray->$count) [/system scheduler get $i comment]
:set ($StartTimeArray->$count) [/system scheduler get $i "start-time"]
:set ($IntervalArray->$count) [/system scheduler get $i interval]
:set count ($count+1);}

:local a ""; :local e; :local b; :local c; :local d; :local t; local v
:local block 0
:for i from=0 to=([:len $ScriptArray]-1) do={:set $c ("$ScriptArray"->"$i"); :set $d ("$CommentArray"->"$i"); set $t ("$StartTimeArray"->"$i"); set $v ("$IntervalArray"->"$i"); 
:if ($StatusArray->"$i") do={:set e "%F0%9F%94%B4"} else={:set e "%F0%9F%94%B7"}
:set b ($i+1);
:set a ("$a"." $e"." $b"." $c"." $d"." [ $t ]"." [ $v ]"."%0A")
# один вывод ограничен количеством nlist символов;
:if ([:len $a]>$nlist) do={:set block ($block+1);
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] scheduler list < $block >:"."%0A%0A"."$a")]
:set a "";
  }
}
:set block ($block+1)
# "допечатка" последнего блока данных
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] scheduler list: < $block >"."%0A%0A"."$a")]
  :return [:len $ScriptArray]
 }
}


# function globalvarlist ->Telegram
# ---
:set globalvarlist do={
:if ([:len $0]!=0) do={
:global Emoji; :global FuncTelegramSender;
:local ScriptArray [:toarray ""];
:local ValArray [:toarray ""];
:local nlist 900
:local name
:local val
:local comment
:local count 0
# seek all global
:foreach i in=[/system script environment find] do={:set $name [/system script environment get $i name]; 
:if (([/system script environment get $i value]!="(code") and  ([:len [:find [/system script environment get $i value] "(eval"]]!=1)) do={
:set $val [/system script environment get $i value];} else={:set val; set name}
:set ($ScriptArray->$count) $name; :set ($ValArray->$count) $val;
:set count ($count+1);
}
:local a ""; :local b 0; :local c; :local d;
:local block 0
:for i from=0 to=([:len $ScriptArray]-1) do={:set $c ("$ScriptArray"->"$i"); :set $d ("$ValArray"->"$i");
:if ([:len $c]!=0) do={:set b ($b+1)
:set a ("$a"." - "."$b  "."$c "."$d"."%0A")}
# один вывод ограничен количеством nlist символов;
:if ([:len $a]>$nlist) do={:set block ($block+1);
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository environment Global var list < $block >:"."%0A%0A"."$a")]
:set a "";
  }
}
:set block ($block+1)
# "допечатка" последнего блока данных
[$FuncTelegramSender ("$Emoji"."$[/system identity get name] repository environment Global var list: < $block >"."%0A%0A"."$a")]
         :return $b;
 }
}


:log info "  -    SATELLITE1 module is set"


